table 'Calculo de Taxas'
	lineageTag: b2b28476-fd1e-4be6-b9b8-e1478d28612b

	measure HORASEXTRAS = ```
			
			VAR DataBase = [DATA_REF]
			VAR AnoComp = YEAR(DataBase)
			VAR MesComp = MONTH(DataBase)
			VAR Resultado =
			    CALCULATE (
			        SUM('Horas Extras'[REF]),
			        'Horas Extras'[ANOCOMP] = AnoComp,
			        'Horas Extras'[MESCOMP] = MesComp
			    )
			
			RETURN
			COALESCE(Resultado, 0)
			
			```
		lineageTag: 561a2e65-dc96-4987-b699-49b1c2a69d30

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure DATA_REF = IF(MAX(Calendario[Date]) > TODAY(), TODAY(), MAX(Calendario[Date]))
		formatString: Short Date
		lineageTag: 8013dfc3-bfa2-4dd7-8a42-57b5467c53c5

	measure TOTAL_DIAS_UTEIS_NO_MES = ```
			
			VAR DataBase  = [DATA_REF]
			VAR InicioMes = DATE(YEAR( DataBase ), MONTH(DataBase), 1)
			VAR FimMes    = EOMONTH (DataBase, 0)
			RETURN
			COUNTROWS(
			    FILTER(
			        CALENDAR(InicioMes, FimMes),
			        WEEKDAY([Date], 2) <= 5
			    )
			)
			
			```
		formatString: 0
		lineageTag: 74ec2379-0d68-40b8-95b6-f458f9e166ee

	measure TOTAL_SABADOS_NO_MES = ```
			
			VAR DataBase  = [DATA_REF]
			VAR InicioMes = DATE(YEAR( DataBase ), MONTH(DataBase), 1)
			VAR FimMes    = EOMONTH (DataBase, 0)
			RETURN
			COUNTROWS(
			    FILTER(
			        CALENDAR(InicioMes, FimMes),
			        WEEKDAY([Date], 2) = 6
			    )
			)
			
			```
		formatString: 0
		lineageTag: 38392629-23d4-4b1c-a89f-16ddf2a80399

	measure TOTAL_FUNCIONARIOS_ATIVOS_NO_MES = ```
			
			VAR DataBase  = [DATA_REF]
			RETURN
			COUNTROWS(
			    FILTER(
			        Funcionarios,
			        Funcionarios[DATAADMISSAO] <= DataBase && (Funcionarios[DATADEMISSAO] > DataBase || ISBLANK(Funcionarios[DATADEMISSAO]))
			    )
			)
			
			```
		formatString: 0
		lineageTag: db213a7b-10c2-441e-ad8d-2325babff8c9

	measure HHT = ```
			
			    (
			        ((([TOTAL_DIAS_UTEIS_NO_MES] * 8) + ([TOTAL_SABADOS_NO_MES] * 4)) * [TOTAL_FUNCIONARIOS_ATIVOS_NO_MES]) - 
			        ((([TOTAL_DIAS_UTEIS_NO_MES] * 8) + ([TOTAL_SABADOS_NO_MES] * 4)) * [TOTAL_FUNCIONARIOS_AFASTADOS_NO_MES])
			    ) + 
			    [HORASEXTRAS]
			
			```
		formatString: 0
		lineageTag: 4b161347-f4e7-40d2-a74f-06bdb658c286

	measure TOTAL_FUNCIONARIOS_AFASTADOS_NO_MES = ```
			
			VAR DataBase = [DATA_REF]
			RETURN
			COUNTROWS (
			    FILTER (
			        ADDCOLUMNS (
			            SUMMARIZE (
			                'Situacao Funcionarios',
			                'Situacao Funcionarios'[CODCOLIGADA],
			                'Situacao Funcionarios'[CHAPA]
			            ),
			            "UltimaSituacao",
			                VAR UltimaData =
			                    CALCULATE (
			                        MAX ( 'Situacao Funcionarios'[DATAMUDANCA] ),
			                        'Situacao Funcionarios'[DATAMUDANCA] <= DataBase
			                    )
			                RETURN
			                    CALCULATE (
			                        MAX ( 'Situacao Funcionarios'[NOVASITUACAO] ),
			                        'Situacao Funcionarios'[DATAMUDANCA] = UltimaData
			                    )
			        ),
			        [UltimaSituacao] IN { "F", "P", "T", "E" }
			    )
			)
			
			```
		formatString: 0
		lineageTag: bed0bf5d-4e07-4480-a5a2-b052eb8a4123

	measure TAXA_DE_FREQUENCIA = COUNTROWS(CATs) * 1000000 / [HHT]
		lineageTag: 40a0051b-103b-4935-b8e7-4d831c23dbca

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure DURACAO_TRATAMENTO_TOTAL = SUM(CATs[DURACAOTRATAMENTO])
		formatString: 0
		lineageTag: e59772e5-6daa-445f-8dae-2769c98c2d07

	measure TAXA_DE_GRAVIDADE = ([DURACAO_TRATAMENTO_TOTAL] + 0) * 1000000 / [HHT]
		lineageTag: 00d59f10-30cf-4d9c-b179-0da9d80acf18

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure HORAS_TRABALHADAS = ([TOTAL_DIAS_UTEIS_NO_MES] * 8) + ([TOTAL_SABADOS_NO_MES] * 4)
		formatString: 0
		lineageTag: f920787a-0ef5-4d6d-ba98-aecaa209297b

	partition 'Calculo de Taxas' = m
		mode: import
		source =
				let
				    Source = Table.FromRows(Json.Document(Binary.Decompress(Binary.FromText("i44FAA==", BinaryEncoding.Base64), Compression.Deflate)), let _t = ((type nullable text) meta [Serialized.Text = true]) in type table [Column1 = _t]),
				    #"Changed Type" = Table.TransformColumnTypes(Source,{{"Column1", type text}}),
				    #"Removed Columns" = Table.RemoveColumns(#"Changed Type",{"Column1"})
				in
				    #"Removed Columns"

	annotation PBI_ResultType = Table

